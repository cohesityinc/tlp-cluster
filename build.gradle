
buildscript {
    ext.kotlin_version = "1.3.41"
    ext.jcommander_version = "1.72"
    
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.1.0'
        classpath "com.netflix.nebula:gradle-ospackage-plugin:6.2.1"
    }
}

// Apply the java plugin to add support for Java
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "nebula.ospackage"
apply plugin: 'nebula.ospackage-application'


group 'com.thelastpickle'
version '0.1'
sourceCompatibility = 1.8

application {
    applicationName = "tlp-cluster"
    mainClassName = "com.thelastpickle.tlpcluster.MainKt"
}

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
    jcenter()
}


// In this section you declare the dependencies for your production and test code
dependencies {

    compile "org.apache.logging.log4j:log4j-api-kotlin:1.0.0"
    compile "org.apache.logging.log4j:log4j-core:2.11.1"
    compile 'org.apache.logging.log4j:log4j-slf4j18-impl:2.11.2'

    // https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-stdlib
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version


    compile group: 'com.beust', name: 'jcommander', version: jcommander_version
    compile 'com.google.guava:guava:27.0-jre'

    // for finding resources
    // https://mvnrepository.com/artifact/org.reflections/reflections
    compile group: 'org.reflections', name: 'reflections', version: '0.9.11'

    // https://mvnrepository.com/artifact/commons-io/commons-io
    compile group: 'commons-io', name: 'commons-io', version: '2.6'

    // https://mvnrepository.com/artifact/com.github.docker-java/docker-java
    compile group: 'com.github.docker-java', name: 'docker-java', version: '3.1.2'

    compile group: 'software.amazon.awssdk', name: 'ec2', version: '2.5.8'

    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.9.0'
    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine

    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.+"

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.1'

    // https://mvnrepository.com/artifact/org.assertj/assertj-core
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.11.1'

    compile 'com.github.ajalt:mordant:1.2.1'

    testCompile "io.mockk:mockk:1.9.3"
}

task docs(type:Exec) {
    environment "TLP_CLUSTER_VERSION", version
    commandLine 'docker-compose', 'up', 'docs'
    group = "Documentation"
    description = "Build website documentation"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs = ["-Xallow-result-return-type"]


}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs = ["-Xallow-result-return-type"]
}



sourceSets {
    main.java.srcDirs += "src/main/kotlin"
    test.java.srcDirs += "src/test/kotlin"
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/kotlin')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    description = "Runs the full end to end tests.  Will create a cluster in AWS.  Errors might require manual cluster tear down."
    group = "Verification"
}

task buildJsonnet(type: Exec) {
    workingDir 'src/dashboards'
    commandLine "docker-compose", "build"
    standardOutput = new ByteArrayOutputStream()
    group = "dashboard"
    description = "Rebuild the jsonnet container"
    //extension method stopTomcat.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }

}

task generateDashboards(type: Exec) {
    workingDir 'src/dashboards'
    commandLine "docker-compose", "up", "--exit-code-from", "grafonnet"


    standardOutput = new ByteArrayOutputStream()
    errorOutput = new ByteArrayOutputStream()

    group = "dashboard"
    description = "Regenerate the Grafana Dashboards using Docker."
    inputs.files(project.fileTree(dir: "src/dashboards"))

    ignoreExitValue true
    doLast {
        logger.info(standardOutput.toString())
        logger.error(errorOutput.toString())
        if (execResult.getExitValue() != 0) {
            print(standardOutput)
            throw new GradleScriptException("non zero exit code")
        }
    }
}

compileKotlin.dependsOn(generateDashboards)

task startPreviewDependencies(type:Exec) {
    group = "dashboard"
    description = "Start Cassandra and wait for it to be up"
    commandLine "docker-compose", "-f", "docker-compose-monitoring-dev.yml", "run", "start_dependencies"
}

task previewDashboards(type:Exec) {
    group = "dashboard"
    description = "Start the dashboard dev environment"
    commandLine "docker-compose", "-f", "docker-compose-monitoring-dev.yml", "up"
    dependsOn startPreviewDependencies
}


test {
    useJUnitPlatform()
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
}

ospackage {
    os = LINUX
    link("/usr/local/bin/tlp-cluster", "/opt/tlp-cluster/bin/tlp-cluster" )
    packager "Jon Haddad"
    maintainer "Jon Haddad"
    vendor "The Last Pickle"
    url "http://thelastpickle.com/tlp-cluster/"
    license "Apache License 2.0"
    description "Cluster Experimentation Tool for Apache Cassandra by The Last Pickle"
}

buildDeb {
    distribution "weezy,bionic,xenial,jessie"
    requires("openjdk-8-jre")
}

task uploadDeb(type: Exec) {
    group = "Publish"
    workingDir 'build/distributions'

    def debPackage = "tlp-cluster_${version}_all.deb"

    // get the deb package

    logger.info("Uploading DEB $debPackage")
    commandLine "curl", "-T", debPackage, "-u", System.getenv("BINTRAY_USER") + ":" + System.getenv("BINTRAY_KEY"), "https://api.bintray.com/content/thelastpickle/tlp-tools-deb/tlp-cluster/${version}/$debPackage;deb_distribution=weezy,bionic,jessie,xenial;deb_component=main;deb_architecture=amd64;publish=1"

}

task uploadRpm(type: Exec) {
    group = "Publish"
    workingDir 'build/distributions'

    def rpmPackage = "tlp-cluster-${version}.noarch.rpm"

    logger.info("Uploading RPM $rpmPackage")
    commandLine "curl", "-T", rpmPackage, "-u", System.getenv("BINTRAY_USER") + ":" + System.getenv("BINTRAY_KEY"), "https://api.bintray.com/content/thelastpickle/tlp-tools-rpm/tlp-cluster/${version}/$rpmPackage;publish=1"
}

task uploadTar(type:Exec) {
    group = "Publish"
    workingDir 'build/distributions'
    def tarball = "tlp-cluster-${version}.tar"

    logger.info("Uploading Tar $tarball")
    commandLine "curl", "-T", tarball, "-u", System.getenv("BINTRAY_USER") + ":" + System.getenv("BINTRAY_KEY"), "https://api.bintray.com/content/thelastpickle/tlp-tools-tarball/tlp-cluster/${version}/$tarball;publish=1"
}

task buildAll {
    dependsOn "buildDeb"
    dependsOn "buildRpm"
    dependsOn "distTar"
}



assemble.mustRunAfter clean